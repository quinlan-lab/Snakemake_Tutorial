### IMPORTS ###
import snakemake 
import os
import re

samples: list[str] = [
    "Sample1",
    "Sample2", 
    "Sample3"
]

wildcard_constraints: 
    sample = "[A-Za-z0-9]+"

rule all:
    input: 
        expand("output/data/{sample}-aligned.bam", sample = samples)

# Software in some rules might need more resources. These can be provided
# threads, which correspond to CPU cores. 
# For example, this rule has 8 threads per instance. If we run
# "snakemake --cores 32", we can have 4 concurrent "fastp" rules running. 

# Some rules might also require explicit memory management. Resources
# decorator can be used to define memory resources. 

# We may also want to provide log output to our running processes. For this, 
# we can define "log" decorator. This has to take the same wildcards 
# as the output. 

# Parameters may also be defined. These don't necessarily need to be files, 
# so these can be useful for dynamically defined variables during runtime in the 
# python code preceding the workflow. Below, we define the average quality 
# needed for filtering. 
rule fastp: 
    input:
        r1 = "../data/{sample}_R1.fastq",
        r2 = "../data/{sample}_R2.fastq"
    output:
        r1_clean = "output/data/{sample}_R1.clean.fastq",
        r2_clean = "output/data/{sample}_R2.clean.fastq",
        html_report = "output/reports/{sample}-fastp-report.html",
        json_report = "output/reports/{sample}-fastp-report.json"
    threads: 
        4
    resources:
        mem_mb = 4000
    log:
        stderr = "logs/{sample}-fastp.log"
    params:
        qual = 20
    shell:
        """
        fastp --in1 {input.r1} --in2 {input.r2} \
        --out1 {output.r1_clean} --out2 {output.r2_clean} \
        --detect_adapter_for_pe \
        --average_qual {params.qual} \
        --thread {threads} \
        --html {output.html_report} \
        --json {output.json_report} 2> {log.stderr}
        """

rule bwa_mem: 
    input:
        r1_clean = "output/data/{sample}_R1.clean.fastq",
        r2_clean = "output/data/{sample}_R2.clean.fastq",
        ref = "../reference/chr1-3.fa"
    output:
        sam = temp("output/data/{sample}-aligned.sam")
    threads: 
        4
    resources: 
        mem_mb = 4000
    log:
        stderr = "logs/{sample}-bwamem.log"
    shell: 
        """
        bwa mem {input.ref} {input.r1_clean} {input.r2_clean} -t {threads} > {output.sam} 2> {log.stderr}
        """

rule convert_sam_to_bam: 
    input:
        sam = "output/data/{sample}-aligned.sam"
    output:
        bam = "output/data/{sample}-aligned.bam"
    threads: 
        4
    resources: 
        mem_mb = 1000
    log:
        stderr = "logs/{sample}_convert.log"
    shell:
        """
        samtools view --bam {input.sam} -o {output.bam} 2> {log.stderr}
        """
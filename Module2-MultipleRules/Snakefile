### IMPORTS ###
import snakemake 
import os
import re

samples: list[str] = [
    "Sample1"
]

# Wildcard constraints; should all be alphanumeric
wildcard_constraints: 
    sample = "[A-Za-z0-9]+"

# Rule 'all', defining all requested output.
rule all:
    input: 
        expand("output/data/{sample}-sorted.bai", sample = samples)

# Rule 'fastp' preprocesses the reads by detecting and removing adapters,
# filtering reads with an average quality less than 20, and producing 
# filtered fastq reads with preprocessing reports.
rule fastp: 
    input:
        r1 = "../data/{sample}_R1.fastq",
        r2 = "../data/{sample}_R2.fastq"
    output:
        r1_clean = "output/data/{sample}_R1.clean.fastq",
        r2_clean = "output/data/{sample}_R2.clean.fastq",
        html_report = "output/reports/{sample}-fastp-report.html",
        json_report = "output/reports/{sample}-fastp-report.json"
    shell:
        """
        fastp --in1 {input.r1} --in2 {input.r2} \
        --out1 {output.r1_clean} --out2 {output.r2_clean} \
        --detect_adapter_for_pe \
        --average_qual 20 \
        --thread {threads} \
        --html {output.html_report} \
        --json {output.json_report}
        """

# Aligns cleaned fastq reads to the defined reference genome. 
rule bwa_mem: 
    input:
        r1_clean = "output/data/{sample}_R1.clean.fastq",
        r2_clean = "output/data/{sample}_R2.clean.fastq",
        ref = "../reference/chr1-3.fa"
    output:
        sam = temp("output/data/{sample}-aligned.sam")
    shell: 
        """
        bwa mem {input.ref} {input.r1_clean} {input.r2_clean} > {output.sam}
        """

# Converts the sam output of bwa mem to bam, only retaining the 
# aligned reads. 
rule convert_sam_to_bam: 
    input:
        sam = "output/data/{sample}-aligned.sam"
    output:
        bam = temp("output/data/{sample}.bam")
    shell:
        """
        samtools view -F 4 --bam {input.sam} -o {output.bam}
        """

rule sort_bam: 
    input:
        bam = "output/data/{sample}.bam"
    output: 
        bam_sort = "output/data/{sample}-sorted.bam"
    shell:
        """
        samtools sort {input.bam} -o {output.bam_sort}
        """

rule index_bam: 
    input: 
        bam_sort = "output/data/{sample}-sorted.bam"
    output:
        bai = "output/data/{sample}-sorted.bai"
    shell:  
        """
        samtools index -b {input.bam_sort} -o {output.bai}
        """
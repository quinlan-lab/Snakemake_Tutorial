### IMPORTS ###
import snakemake 
import os
import re

samples: list[str] = [
    "Sample1",
    "Sample2", 
    "Sample3"
]

wildcard_constraints: 
    sample = "[A-Za-z0-9]+"

# For this, we'll add a few more rules; sort_bam, qc, and index_bam. 
# qc and index_bam both require the output of sort_bam. In this case, these 
# rules can branch and run in parallel to each other, since they are not dependent
# on the successful completion of each other. 
# However, since there are now two "ending" data types, we need to have two expansions 
# in the "all" rule. See the DAG for information on this. 
rule all:
    input: 
        expand("output/data/{sample}.bai", sample = samples),
        expand("output/reports/{sample}_fastqc.html", sample = samples)

rule fastp: 
    input:
        r1 = "../data/{sample}_R1.fastq",
        r2 = "../data/{sample}_R2.fastq"
    output:
        r1_clean = "output/data/{sample}_R1.clean.fastq",
        r2_clean = "output/data/{sample}_R2.clean.fastq",
        html_report = "output/reports/{sample}-fastp-report.html",
        json_report = "output/reports/{sample}-fastp-report.json"
    threads: 
        4
    resources:
        mem_mb = 4000
    log:
        stderr = "logs/{sample}-fastp.log"
    params:
        qual = 20
    shell:
        """
        fastp --in1 {input.r1} --in2 {input.r2} \
        --out1 {output.r1_clean} --out2 {output.r2_clean} \
        --detect_adapter_for_pe \
        --average_qual {params.qual} \
        --thread {threads} \
        --html {output.html_report} \
        --json {output.json_report} 2> {log.stderr}
        """

rule bwa_mem: 
    input:
        r1_clean = "output/data/{sample}_R1.clean.fastq",
        r2_clean = "output/data/{sample}_R2.clean.fastq",
        ref = "../reference/chr1-3.fa"
    output:
        sam = temp("output/data/{sample}-aligned.sam")
    threads: 
        4
    resources: 
        mem_mb = 4000
    log:
        stderr = "logs/{sample}-bwamem.log"
    shell: 
        """
        bwa mem {input.ref} {input.r1_clean} {input.r2_clean} -t {threads} > {output.sam} 2> {log.stderr}
        """

rule convert_sam_to_bam: 
    input:
        sam = "output/data/{sample}-aligned.sam"
    output:
        bam = temp("output/data/{sample}-aligned.bam")
    threads: 
        4
    resources: 
        mem_mb = 1000
    log:
        stderr = "logs/{sample}_convert.log"
    shell:
        """
        samtools view -F 4 --bam {input.sam} -o {output.bam} 2> {log.stderr}
        """

rule sort_bam: 
    input:
        bam = "output/data/{sample}-aligned.bam"
    output: 
        bam_sort = "output/data/{sample}.bam"
    threads: 
        4
    resources:
        mem_mb = 4000
    log:
        stderr = "output/logs/{sample}-sort.log"
    shell:
        """
        samtools sort {input.bam} -o {output.bam_sort} 2> {log.stderr}
        """

# Note that you do not need to specify the output variable in the shell script; 
# in this case, this is the output file that we expect the rule to produce. 
# As long as the file is produced after the execution of the rule, then it will 
# work. 
rule qc:
    input:
        bam_sort = "output/data/{sample}.bam"
    output:
        qc = "output/reports/{sample}_fastqc.html"
    threads: 
        4
    resources: 
        mem_mb = 1000
    log:
        stderr = "output/logs/{sample}-qc.log"
    params:
        qc_dir = "output/reports"
    shell:
        """
        fastqc {input.bam_sort} -o {params.qc_dir} 2> {log.stderr}
        """

rule index_bam: 
    input: 
        bam_sort = "output/data/{sample}.bam"
    output:
        bai = "output/data/{sample}.bai"
    threads: 
        4
    log:
        stderr = "output/logs/{sample}-indexbam.log"
    shell:  
        """
        samtools index -b {input.bam_sort} -o {output.bai} 2> {log.stderr}
        """